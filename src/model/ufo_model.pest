COMMENT = _{single_line_comment}
single_line_comment = { "#" ~ (!NEWLINE ~ ANY)* }
WHITESPACE = _{" " | "\t" | NEWLINE}

name = @{ (ASCII_ALPHANUMERIC | "_")+ }
int = @{"-"? ~ (!WHITESPACE ~ ASCII_DIGIT)+}
rational = @{int ~ ("/" ~ int)?}
string = @{("\"" | "\'") ~ (!("\"" | "\'") ~ ASCII)* ~ ("\"" | "\'")}
bool = @{^"true" | ^"false"}
int_string_combination = {string ~ ":" ~ int}
string_int_dict = _{"{" ~ (!"}" ~ int_string_combination)+ ~ "}"}
coupling_dict = _{"{" ~ (!"}" ~ ("(" ~ (!")" ~ (ASCII_DIGIT+ ~ ","?)+) ~ ")" ~ ":" ~ "C." ~ name) ~ ","?)* ~ "}"}

property_pdg_code = {^"pdg_code" ~ "=" ~ int}
property_name = {^"name" ~ "=" ~ string}
property_antiname = {^"antiname" ~ "=" ~ string}
property_spin = {^"spin" ~ "=" ~ int}
property_color = {^"color" ~ "=" ~ int}
property_mass = {^"mass" ~ "=" ~ (!("," | ")") ~ ANY)*}
property_width = {^"width" ~ "=" ~ (!("," | ")") ~ ANY)*}
property_texname = {^"texname" ~ "=" ~ string}
property_antitexname = {^"antitexname" ~ "=" ~ string}
property_charge = {^"charge" ~ "=" ~ rational}
property_GhostNumber = {^"GhostNumber" ~ "=" ~ int}
property_LeptonNumber = {^"LeptonNumber" ~ "=" ~ int}
property_Y = {^"Y" ~ "=" ~ int}
property_goldstone = {^"goldstone" ~ "=" ~ bool}
property_propagating = {^"propagating" ~ "=" ~ bool}
property_line = {^"line" ~ "=" ~ string}
property_misc = {ASCII* ~ "=" ~ (!("," | ")") ~ ANY)*}


particle_property = _{
    property_pdg_code |
    property_name |
    property_antiname |
    property_spin |
    property_color |
    property_mass |
    property_width |
    property_texname |
    property_antitexname |
    property_charge |
    property_GhostNumber |
    property_LeptonNumber |
    property_Y |
    property_goldstone |
    property_propagating |
    property_line |
    property_misc
}

particle = { name ~ "=" ~ "Particle(" ~ (!")" ~ particle_property ~ ","? ~ NEWLINE?)* ~ ")" }
anti_particle = {name ~ "=" ~ name ~ ".anti()"}

particles_py = {
    SOI ~ ((!(particle | anti_particle) ~ ANY)* ~ (particle | anti_particle))* ~ ANY* ~ EOI
}

property_coupling_order = {^"expansion_order" ~ "=" ~ int}
property_hierarchy = {^"hierarchy" ~ "=" ~ int}

coupling_order_property = _{ property_name | property_coupling_order | property_hierarchy }

coupling_order = { name ~ "=" ~ "CouplingOrder(" ~ (!")" ~ coupling_order_property ~ ","? ~ NEWLINE?)* ~ ")" }

coupling_orders_py = {SOI ~ ((!coupling_order ~ ANY)* ~ coupling_order)* ~ ANY* ~ EOI}

property_value = {^"value" ~ "=" ~ string}
property_order = {^"order" ~ "=" ~ string_int_dict}

coupling_property = _{ property_name | property_value | property_order }

coupling = { name ~ "=" ~ "Coupling(" ~ (!")" ~ coupling_property ~ ","? ~ NEWLINE?)* ~ ")" }

couplings_py = {SOI ~ ((!coupling ~ ANY)* ~ coupling)* ~ ANY* ~ EOI}

property_particles = {^"particles" ~ "=" ~ "[" ~ (!"]" ~ "P." ~ name ~ ","?)* ~ "]"}
property_vertex_color = {^"color" ~ "=" ~ "[" ~ (!"]" ~ ANY)* ~ "]"}
property_lorentz = {^"lorentz" ~ "=" ~ "[" ~ (!"]" ~ ANY)* ~ "]"}
property_couplings = {^"couplings" ~ "=" ~ coupling_dict}

vertex_property = _{ property_name | property_particles | property_lorentz | property_vertex_color | property_couplings}

vertex = {name ~ "=" ~ "Vertex(" ~ (!")" ~ vertex_property ~ ","? ~ NEWLINE?)* ~ ")"}

vertices_py = {SOI ~ ((!vertex ~ ANY)* ~ vertex)* ~ ANY* ~ EOI}